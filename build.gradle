plugins {
    id "org.jetbrains.intellij" version "0.2.17"
}

intellij {
    version '2016.3'
}

patchPluginXml {
    untilBuild = "181.*"
}

apply plugin: 'java'

sourceSets {
    main {
        java.srcDirs 'src'
        resources.srcDirs 'resources'
    }
    test {
        java.srcDir 'test'
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

configurations {
    codacy
}

dependencies {
    codacy group: 'com.codacy', name: 'codacy-coverage-reporter', version: '2.0.0'
}

apply plugin: "jacoco"

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task uploadCoverageToCodacy(type: JavaExec, dependsOn : jacocoTestReport) {
    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "-l",
            "Java",
            "-r",
            "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    ]
}

task (codacyDepsize) << {
    def size = 0;
    configurations.codacy.collect { it.length() / (1024 * 1024) }.each { size += it }
    println "Total dependencies size: ${Math.round(size * 100) / 100} Mb"

    configurations
            .codacy
            .sort { -it.length() }
            .each { println "${it.name} : ${Math.round(it.length() / (1024) * 100) / 100} kb" }
}

task (codacyLocs) << {
    configurations.codacy.each {
        String jarName = it
        println jarName
    }
}